
#Область ПрограммныйИнтерфейс

#Область РаботаСТипомЦвет

// Возвращает массив web-цветов
// 
// Возвращаемое значение:
//   - Массив - массив всех цветов из набора WebЦвета.
//
Функция ПолучитьWebЦвета() Экспорт
	
	Результат = Новый Массив;
	
	Для ИндексВнутр = 1 По 146 Цикл
		
		Цвет = ЗначениеИзСтрокиВнутр(
			"{""#"",9cd510c7-abfc-11d4-9434-004095e12fc7,2,{3,2,{" + ИндексВнутр + "}}}");
		
		Результат.Добавить(Цвет);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращаем массив цветов стиля
// 
// Возвращаемое значение:
//   - Массив - массив всех цветов из набора ЦветаСтиля.
//
Функция ПолучитьЦветаСтиля() Экспорт
	
	Результат = Новый Массив;
	
	Для ИндексВнутр = 1 По 47 Цикл
		
		Цвет = ЗначениеИзСтрокиВнутр(
			"{""#"",9cd510c7-abfc-11d4-9434-004095e12fc7,2,{3,3,{" + ( - ИндексВнутр) + "}}}");
		
		Если СтрРазделить(СокрЛП(Цвет), ":", Ложь).Количество() <= 1 Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(Цвет);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив Windows-цветов
// 
// Возвращаемое значение:
//   - Массив - массив всех цветов из набора WindowsЦвета.
//
Функция ПолучитьWindowsЦвета() Экспорт
	
	Результат = Новый Массив;
	
	ДиапазоныИндексов = Новый Массив;
	ДиапазоныИндексов.Добавить(Новый Структура("Начало, Конец, Множитель", 0, 24, 1));
	ДиапазоныИндексов.Добавить(Новый Структура("Начало, Конец, Множитель", 26, 28, 1));
	ДиапазоныИндексов.Добавить(Новый Структура("Начало, Конец, Множитель", 1, 2, - 1));
	
	Для Каждого Диапазон Из ДиапазоныИндексов Цикл
		
		Для ИндексВнутр = Диапазон.Начало По Диапазон.Конец Цикл
			Цвет = ЗначениеИзСтрокиВнутр(
				"{""#"",9cd510c7-abfc-11d4-9434-004095e12fc7,2,{3,1,{"
					+ Диапазон.Множитель * ИндексВнутр + "}}}");
				
			Результат.Добавить(Цвет);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Определяет абсолютный цвет для переданного цвета любого вида
//
// Параметры:
//  Цвет - Цвет	 - цвет любого вида.
// 
// Возвращаемое значение:
//   - Цвет - цвета вида Абсолютный.
//   - Неопределено
//
Функция ОпределитьАбсолютныйЦвет(Цвет) Экспорт
	
	Если Цвет.Вид = ВидЦвета.Абсолютный Тогда
		Возврат Цвет;
	КонецЕсли;
	
	ИндексВнутр = Число(СтрЗаменить(Сред(ЗначениеВСтрокуВнутр(Цвет), 53, 3), "}", ""));
	
	Если Цвет.Вид = ВидЦвета.WebЦвет Тогда
		ИндексВнутр = ИндексВнутр - 1;
		ЦветаRGB = ПолучитьПоследовательностьRGBДляWebЦветов1С();
	ИначеЕсли Цвет.Вид = ВидЦВета.ЭлементСтиля Тогда
		ИндексВнутр = - ИндексВнутр - 1;
		ЦветаRGB = ПолучитьПоследовательностьRGBДляЦветовСтиля1С();
	ИначеЕсли Цвет.Вид = ВидЦвета.WindowsЦвет Тогда
		ИндексВнутр = ИндексВнутр + 2;
		ЦветаRGB = ПолучитьПоследовательностьRGBДляWindowsЦветов1С();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	СтрокаRGB = Сред(ЦветаRGB, ИндексВнутр * 10 + 1, 9);
	
	Если СтрокаRGB = "" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	АбсЦвет = Новый Цвет(Сред(СтрокаRGB, 1, 3), Сред(СтрокаRGB, 4, 3), Сред(СтрокаRGB, 7));
	
	Возврат АбсЦвет;
	
КонецФункции

// Пытается определить web-цвет для переданного цвета любого вида
//
// Параметры:
//  Цвет - Цвет	 - цвет любого вида.
// 
// Возвращаемое значение:
//   - Цвет - цвет вида WebЦвет.
//   - Неопределено
//
Функция НайтиWebЦвет(Цвет) Экспорт
	Возврат ПривестиЦветКВиду(Цвет, ВидЦвета.WebЦвет);
КонецФункции

// Пытается определить цвет стиля для переданного цвета любого вида
//
// Параметры:
//  Цвет - Цвет	 - цвет любого вида.
// 
// Возвращаемое значение:
//   - Цвет - цвет вида ЦветСтиля.
//   - Неопределено
//
Функция НайтиЦветСтиля(Цвет) Экспорт
	
	Возврат ПривестиЦветКВиду(Цвет, ВидЦвета.ЭлементСтиля);
	
КонецФункции

// Пытается определить windows-цвет для переданного цвета любого вида
//
// Параметры:
//  Цвет - Цвет	 - цвет любого вида.
// 
// Возвращаемое значение:
//   - Цвет - цвет вида WindowsЦвет.
//   - Неопределено
//
Функция НайтиWindowsЦвет(Цвет) Экспорт
	
	Возврат ПривестиЦветКВиду(Цвет, ВидЦвета.WindowsЦвет);
	
КонецФункции

#КонецОбласти

#Область РаботаСДеревомЗначений

Процедура ДеревоЗначенийИзТаблицыЗначений(ТаблицаЗначений, ИмяКолонкиИдентификатораСтроки,
		ИмяКолонкиИдентификатораРодителяСтроки) Экспорт
	
	ТаблицаЗначений.Сортировать(ИмяКолонкиИдентификатораРодителяСтроки);
	
	ТаблицаЗначений.Индексы.Добавить(ИмяКолонкиИдентификатораСтроки)
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСТипомЦвет

Функция ПривестиЦветКВиду(Цвет, ВидПриведенияЦвета)
	
	Если Цвет.Вид = ВидПриведенияЦвета Тогда
		Возврат Цвет;
	КонецЕсли;
	
	АбсЦвет = ОпределитьАбсолютныйЦвет(Цвет);
	
	Если ВидПриведенияЦвета = ВидЦвета.Абсолютный Тогда
		Возврат АбсЦвет;
	КонецЕсли;
	
	Множитель = 1;
	Сдвиг = 1;
	
	Если ВидПриведенияЦвета = ВидЦвета.WebЦвет Тогда
		ЦветаRGB = ПолучитьПоследовательностьRGBДляWebЦветов1С();
		ПризнакМетаданных = "2";
	ИначеЕсли ВидПриведенияЦвета = ВидЦвета.ЭлементСтиля Тогда
		ЦветаRGB = ПолучитьПоследовательностьRGBДляЦветовСтиля1С();
		ПризнакМетаданных = "3";
		Множитель = - 1;
	ИначеЕсли ВидПриведенияЦвета = ВидЦвета.WindowsЦвет Тогда
		ЦветаRGB = ПолучитьПоследовательностьRGBДляWindowsЦветов1С();
		ПризнакМетаданных = "1";
		Сдвиг = - 1;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	ФС = "ЧЦ=3; ЧДЦ=0; ЧН=000; ЧВН=";
	
	СтрокаRGB = Формат(АбсЦвет.Красный, ФС) + Формат(АбсЦвет.Зеленый, ФС)
		+ Формат(АбсЦвет.Синий, ФС);
		
	Позиция = СтрНайти(ЦветаRGB, СтрокаRGB);
	
	Если Позиция = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИндексВнутр = Множитель * ((Позиция - 1) / 10 + Сдвиг);
	
	СтрокаВнутр = "{""#"",9cd510c7-abfc-11d4-9434-004095e12fc7,2,{3,"
		+ ПризнакМетаданных + ",{" + ИндексВнутр + "}}}";
		
	Результат = ЗначениеИзСтрокиВнутр(СтрокаВнутр);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьПоследовательностьRGBДляWebЦветов1С()
	Возврат
	"240248255 250235215 000255255 127255212 240255255 245245220 255228196 000000000 255235205
	|000000255 138043226 165042042 222184135 095158160 127255000 210105030 255127080 100149237
	|255248220 255251240 220020060 000255255 000000139 000139139 184134011 169169169 000100000
	|189183107 139000139 085107047 255140000 153050204 139000000 233150122 143188139 072061139
	|047079079 000206209 148000211 255020147 000191255 105105105 030144255 178034034 255250240
	|034139034 255000255 220220220 248248255 255215000 218165032 128128128 000128000 173255047
	|240255240 255105180 205092092 075000130 255255240 240230140 230230250 255240245 124252000
	|255250205 166202240 240128128 224255255 255236139 250250210 144238144 192192192 255182193
	|255160122 032178170 135206250 132112255 119136153 176196222 255255224 000255000 050205050
	|250240230 255000255 128000000 102205170 000000205 160160160 192220192 186085211 147112219
	|060179113 123104238 000250154 072209204 199021133 025025112 245255250 255228225 255228181
	|255222173 000000128 253245230 128128000 107142035 255165000 255069000 218112214 238232170
	|152251152 175238238 219112147 255239213 255218185 205133063 255192203 221160221 176224230
	|128000128 255000000 188143143 065105225 139069019 250128114 244164096 046139087 255245238
	|160082045 192192192 135206235 106090205 112128144 255250250 000255127 070130180 210180140
	|000128128 216191216 255099071 064224208 238130238 208032144 245222179 255255255 245245245
	|255255000 154205050";
КонецФункции

Функция ПолучитьПоследовательностьRGBДляЦветовСтиля1С()
	Возврат
	"255255255 XXXXXXXXX 051051051 XXXXXXXXX XXXXXXXXX XXXXXXXXX 255255255 XXXXXXXXX XXXXXXXXX
	|255255255 051051051 XXXXXXXXX 245251247 083106194 255255255 255000000 255000000 XXXXXXXXX
	|XXXXXXXXX XXXXXXXXX 077077077 160160160 255250217 000000000 244236197 248242216 251249236
	|204192133 XXXXXXXXX XXXXXXXXX XXXXXXXXX XXXXXXXXX XXXXXXXXX 178178178 250250250 077077077
	|250250250 077077077 XXXXXXXXX XXXXXXXXX XXXXXXXXX 251237158 255255217 250204031 XXXXXXXXX
	|000150070 255225000";
КонецФункции

Функция ПолучитьПоследовательностьRGBДляWindowsЦветов1С()
	Возврат
	"191205219 153180209 200200200 000000000 153180209 191205219 240240240 255255255 180180180
	|000000000 000000000 000000000 180180180 244247252 171171171 000120215 255255255 240240240
	|160160160 109109109 000000000 000000000 255255255 105105105 227227227 000000000 255255225
	|XXXXXXXXX 000120215 153180209 191205219";
КонецФункции

#КонецОбласти

#КонецОбласти
